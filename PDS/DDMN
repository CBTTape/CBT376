//C4SYSDMA JOB MSGCLASS=X,CLASS=B,NOTIFY=XSDMN
//*
// EXEC ASMHCL,PARM.L=('AMODE=31','RMODE=24')
//C.SYSLIB DD DISP=SHR,DSN=SYS1.MACLIB
//         DD DISP=SHR,DSN=SYS1.MODGEN
//SYSPRINT DD SYSOUT=*
//SYSIN DD *
DDMN     CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         USING *,R15
         STM   R14,R12,12(R13)
         LA    R12,SAVE
         ST    R12,8(R13)
         ST    R13,SAVE+4
         LR    R13,R12
         LR    R12,R15
         USING DDMN,R12
         DROP  R15
INIT     L     R2,16               (R2)=ADDR OF CVT                     00022
         USING CVT,R2
         L     R5,CVTOPCTP         (R5)=ADDR OF RMCT                    00023
         L     R3,RMCTDMDT(R5)     (R3)=ADDR OF DMDT (FIRST ENTRY)      00024
         XR    R4,R4               CLEAR REG 4                          00025
         ICM   R4,B'0011',RMCTDMNC(R5)   (R4)=NUMBER OF DOMAINS         00026
         LTR   R4,R4               ANYBODY HOME?                        00027
         BNP   ENDJC                NOPE-> EXIT STAGE LEFT              00028
         USING DMDT,R3             ESTABLISH ADDR TO DOMAIN DESC. TABLE 00029
         SPACE                                                          00030
         L     R5,RMCTWMST(R5)     (R5)=ADDR WMST                       00031
         MVC   MIPS,4(R5)          MOVE IN IPS NUMBER TO MSGHDR1        00032
         LA    R1,MSGHDR1                                               00033
         BAL   R6,PUTMSG                                                00034
         LA    R1,MSGHDR2          POKE OUT THE HEADER MESSAGES         00035
         BAL   R6,PUTMSG                                                00036
         EJECT                                                          00037
LOOP     MVI   MSG,C' '            CLEAR OUT THE MESSAGE BUFFER         00038
         MVC   MSG+1(L'MSG-1),MSG    WITH BLANKS                        00039
         LA    R1,DMDTNO                                                00040
         LA    R2,MNO                                                   00041
         BAL   R6,EDIT             GO EDIT DOMAIN NUMBER INTO MSG       00042
         CLI   MNO+2,C'-'          DOMAIN=0?                            00043
         BNE   CONTINUE             NO-> SKIP FIXUP                     00044
         MVI   MNO+2,C'0'                                               00045
CONTINUE DS    0H                                                       00046
         SPACE                                                          00047
         LA    R1,DMDTLO                                                00048
         LA    R2,MLO                                                   00049
         BAL   R6,EDITH            GO EDIT MIN MPL INTO MSG             00050
         SPACE                                                          00051
         LA    R1,DMDTHI                                                00052
         LA    R2,MHI                                                   00053
         BAL   R6,EDITH            GO EDIT MAX MPL INTO MSG             00054
         SPACE                                                          00055
*        LA    R1,DMDTWT                                                00056
*        LA    R2,MWT                                                   00057
*        BAL   R6,EDIT             GO EDIT WEIGHTING FACTOR INTO MSG    00058
         SPACE                                                          00059
         LA    R1,DMDTMPLI                                              00060
         LA    R2,MMPLT                                                 00061
         BAL   R6,EDITH            GO EDIT CURRENT MPL TARGET INTO MSG  00062
         SPACE                                                          00063
         LA    R1,DMDTRUA                                               00064
         LA    R2,MRUA                                                  00065
         BAL   R6,EDITRUA          GO EDIT AVE # OF READY USRS INTO MSG 00066
         SPACE                                                          00067
         LA    R1,DMDTMPLO                                              00068
         LA    R2,MGOOU                                                 00069
         BAL   R6,EDITH            GO EDIT CURRENT # USRS GOING OUT     00070
         SPACE                                                          00071
         LA    R1,DMDTCMPL                                              00072
         LA    R2,MCMPL                                                 00073
         BAL   R6,EDITH            GO EDIT CURRENT MPL INTO MSG         00074
         SPACE                                                          00075
         LA    R1,DMDTOUTU                                              00076
         LA    R2,MOUTU                                                 00077
         BAL   R6,EDITH            GO EDIT CURNT # USRS ON OUT QUEUE    00078
         SPACE                                                          00079
         LA    R1,DMDTTWSR                                              00080
         LA    R2,SERVR                                                 00081
         BAL   R6,EDITH1S          GO EDIT SERVICE                      00082
         SPACE                                                          00079
         LA    R1,DMDTCIDX                                              00080
         LA    R2,CIDXR                                                 00081
         BAL   R6,EDITH1D          GO EDIT CIDX                         00082
         SPACE                                                          00079
         LA    R1,DMDTINCU                                              00080
         LA    R2,MINCU                                                 00081
         BAL   R6,EDITH            GO EDIT CURNT # SWAPBL IN CORE USERS 00082
         SPACE                                                          00083
         LA    R1,WTOMSG                                                00084
         BAL   R6,PUTMSG                                                00085
         SPACE 2                                                        00086
         LA    R3,DMDTLEN(R3)      BUMP POINTER                         00087
         BCT   R4,LOOP             LOOP THROUGH ALL DOMAINS             00088
*                                                                       00089
         B     ENDJC                                                    00090
         EJECT                                                          00091
*.....................................................................* 00092
*     LOCAL SUBROUTINES   EDIT,EDITH  (USES REG 5)                    * 00093
*.....................................................................* 00094
         SPACE                                                          00095
EDIT     SR    R5,R5               CLEAR WORK REGISTER                  00096
         ICM   R5,B'0001',0(R1)    GET BYTE TO CONVERT                  00097
         C     R5,=F'999'          TO LARGE?                            00098
         BNH   EDIT1                                                    00099
         MVC   0(3,R2),=C'***'     INDICATE TOO LARGE                   00100
         BR    R6                    AND RETURN                         00101
EDIT1    CVD   R5,CVDWORK          CONVERT TO DECIMAL                   00102
         MVC   0(3,R2),=X'202020'  MOVE IN MASK                         00103
         BCTR  R2,0                DECREMENT POSITION FOR SPACE         00104
         ED    0(4,R2),CVDWORK+6     BEFORE NUMBER AND EDIT             00105
         CLC   1(3,R2),=C'   '     ANYTHING THERE                       00106
         BE    EDIT2                                                    00107
         BR    R6                                                       00108
EDIT2    MVI   3(R2),C'-'          INDICATE PLACE HOLDER                00109
         BR    R6                  RETURN                               00110
         SPACE 3                                                        00111
EDITRUA  SR    R5,R5               CLEAR WORK REGISTER                  00112
         ICM   R5,B'0011',0(R1)    GET HALFWORD TO CONVERT              00113
         ST    R5,RUA
         SRL   R5,4
         C     R5,=F'999'          TO LARGE?                            00114
         BNH   EDITRUX                                                  00115
         MVC   0(5,R2),=C'*****'   INDICATE TOO LARGE                   00116
         BR    R6                    AND RETURN                         00117
EDITRUX  CVD   R5,CVDWORK          CONVERT TO DECIMAL                   00118
         MVC   0(3,R2),=X'202021'  MOVE IN MASK                         00119
         BCTR  R2,0                DECREMENT POSITION FOR SPACE         00120
         ED    0(4,R2),CVDWORK+6     BEFORE NUMBER AND EDIT             00121
         MVC   4(1,R2),=C'.'
         L     R5,RUA
         N     R5,=F'15'
         LA    R2,4(R2)
         CVD   R5,CVDWORK          CONVERT TO DECIMAL                   00118
*        MVC   1(1,R2),=X'20'      MOVE IN MASK                         00119
*        ED    1(1,R2),CVDWORK+7     BEFORE NUMBER AND EDIT             00121
         BR    R6                                                       00124
EDITH    SR    R5,R5               CLEAR WORK REGISTER                  00112
         ICM   R5,B'0011',0(R1)    GET HALFWORD TO CONVERT              00113
         C     R5,=F'999'          TO LARGE?                            00114
         BNH   EDITH1                                                   00115
         MVC   0(3,R2),=C'***'     INDICATE TOO LARGE                   00116
         BR    R6                    AND RETURN                         00117
EDITH1D  SR    R5,R5               CLEAR WORK REGISTER                  00112
         ICM   R5,B'0011',0(R1)    GET HALFWORD TO CONVERT              00113
         C     R5,=F'9999999'      TO LARGE?                            00114
         BNH   EDITH1X                                                  00115
         MVC   0(8,R2),=C'********' INDICATE TOO LARGE                  00116
         BR    R6                    AND RETURN                         00117
EDITH1X  CVD   R5,CVDWORK          CONVERT TO DECIMAL                   00118
         MVC   0(8,R2),=X'20202021204B2020'  MOVE IN MASK               00119
         BCTR  R2,0                DECREMENT POSITION FOR SPACE         00104
         ED    0(9,R2),CVDWORK+4     BEFORE NUMBER AND EDIT             00121
         BR    R6
EDITH1S  SR    R5,R5               CLEAR WORK REGISTER                  00112
         L     R5,0(R1)            LOAD                                 00113
         C     R5,=F'99999999'     TO LARGE?                            00114
         BNH   EDITH1Y                                                  00115
         MVC   0(8,R2),=C'********' INDICATE TOO LARGE                  00116
         BR    R6                    AND RETURN                         00117
EDITH1Y  CVD   R5,CVDWORK          CONVERT TO DECIMAL                   00118
         MVC   0(8,R2),=X'2020202020202120'  MOVE IN MASK               00119
         BCTR  R2,0                DECREMENT POSITION FOR SPACE         00104
         ED    0(9,R2),CVDWORK+4     BEFORE NUMBER AND EDIT             00121
         BR    R6
EDITH1   CVD   R5,CVDWORK          CONVERT TO DECIMAL                   00118
         MVC   0(3,R2),=X'202020'  MOVE IN MASK                         00119
         BCTR  R2,0                DECREMENT POSITION FOR SPACE         00120
         ED    0(4,R2),CVDWORK+6     BEFORE NUMBER AND EDIT             00121
         CLC   1(3,R2),=C'   '     ANYTHING THERE                       00122
         BE    EDITH2                                                   00123
         BR    R6                                                       00124
EDITH2   MVI   3(R2),C'-'          INDICATE PLACE HOLDER                00125
         BR    R6                  RETURN                               00126
         EJECT                                                          00127
*.....................................................................* 00128
*        LOCAL SUBROUTINE FOR IO TO OPER/USER                         * 00129
*.....................................................................* 00130
         SPACE                                                          00131
*                                                                       00132
* ENTER WITH WTO,WTOR LIST FORM POINTED TO BY R1                        00133
*                                                                       00134
*    BAL  R6,PUTMSG         OR                                          00135
*    BAL  R6,PUTGET                                                     00136
*                                                                       00137
*  USES R14 FOR WORK                                                    00138
*                                                                       00139
*                                                                       00140
PUTMSG   DS    0H                                                       00141
TPUTIT   DS    0H                                                       00146
         LH    R0,0(,R1)           GET LENGTH OF WTO MESSAGE            00147
         S     R0,=F'4'            SUBTRACT OFF HEADER                  00148
         LA    R1,4(,R1)           BUMP MSG ADDRESS PAST HDR            00149
         TPUT  (1),(0),R           DO THE TPUT                          00150
         BR    R6                                                       00151
         SPACE 2                                                        00152
*.....................................................................* 00185
*        VARIABLES                                                    * 00186
*.....................................................................* 00187
         SPACE 2                                                        00188
CVDWORK  DC    D'0'                WORK AREA                            00189
ANSWER   DC    F'0'                ADDRESS POINTER FOR EXTRACT          00190
TSOFLAG  DC    X'00'               ='80' IF TSO. '00' IF NOT TSO        00191
*                                                                       00192
EXTRACT  EXTRACT ANSWER,'S',FIELDS=(TSO),MF=L                           00199
         EJECT                                                          00200
*.....................................................................* 00240
*        LITERAL POOL                                                 * 00241
*.....................................................................* 00242
         SPACE                                                          00243
         LTORG                                                          00244
         EJECT                                                          00245
ENDJC    DS    0H
         L     R13,4(R13)
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
SYSPRINT DCB   DSORG=PS,MACRF=(PM),DDNAME=SYSPRINT,LRECL=79,RECFM=FB,  X
               BLKSIZE=790
SAVE     DC    19F'0'
*.....................................................................* 00201
*        MESSAGES AND MESSAGE MAPPING OVERLAYS                        * 00202
*.....................................................................* 00203
         SPACE                                                          00204
RUA      DS    1F
MSGHDR1  WTO   'IPS=XX                                    ',           X00205
               ROUTCDE=(11),MF=L                                        00206
         ORG   MSGHDR1+8                                                00207
MIPS     DS    CL2                                                      00208
         ORG                                                            00209
MSGHDR2  WTO   'DOM CUR TGT MIN MAX   RDY OUT SERVICE      CIDX  IN >O'X00210
               ,ROUTCDE=(11),MF=L                                       00211
WTOMSG   WTO   '                                                     ',X00214
               ROUTCDE=(11),MF=L                                        00215
         ORG   WTOMSG+4                                                 00216
MSG      DS    CL43                                                     00217
         ORG   WTOMSG+4                                                 00218
MNO      DS    CL3                                                      00219
         DS    CL1
MCMPL    DS    CL3                                                      00227
         DS    CL1
MMPLT    DS    CL3                                                      00229
         DS    CL1
MLO      DS    CL3                                                      00221
         DS    CL1
MHI      DS    CL3                                                      00223
         DS    CL1
MRUA     DS    CL5                                                      00231
         DS    CL1
MOUTU    DS    CL3                                                      00237
         DS    CL1
SERVR    DS    CL8                                                      00233
         DS    CL1
CIDXR    DS    CL8                                                      00233
         DS    CL1
MINCU    DS    CL3                                                      00233
         DS    CL1
MGOOU    DS    CL3                                                      00235
         ORG                                                            00238
*                                                                       00239
RMCTDMDT EQU   X'B4'           POINTER TO DOMAIN TABLE                  00269
RMCTDMNC EQU   X'BC'           COUNT OF NUMBER OF DOMAINS               00270
RMCTWMST EQU   X'18'           ADDRESS OF WLM SPEC TABLE                00271
*.....................................................................* 00246
*        MAPPING DSECT FOR DMDT (DOMAIN DESCRIPTOR TABLE)             * 00247
*.....................................................................* 00248
         SPACE                                                          00249
DMDT     DSECT                                                          00250
DMDTNO   DS    XL1             DOMAIN NUMBER                            00251
DMDTRSV0 DS    XL1             RESERVED                                 00251
DMDTMPLI DS    XL2             MPL IN TARGET                            00252
DMDTMPLO DS    XL2             MPL OUT TARGET                           00252
DMDTRUA  DS    XL2             AVE # OF READY USERS                     00256
DMDTFITS DS    XL2             FITS RC                                  00256
DMDTCMPL DS    XL2             CURRENT MPL                              00258
DMDTOUTU DS    XL2             CURRENT # USERS ON OUT Q                 00259
DMDTINCU DS    XL2             CURRENT # SWAPPABLE INCORE USERS         00260
DMDTRUC  DS    XL4             ACCUM FOR READY USER AVERAGE             00261
DMDTWMS  DS    XL4             INTVL DMN SVCE ACCUM                     00261
DMDTTWSR DS    XL4             WEIGHTED INTVL DMN SVCE                  00261
DMDTRSV2 DS    XL4             RESERVED                                 00251
DMDTCIDX DS    XL2             CONTENTION INDEX                         00261
DMDTNSW  DS    XL2             CURRENT AND N/S IN USERS                 00261
DMDTRUMX DS    XL2             MAX AND READY USERS IN INTERVAL          00261
DMDTFLGS DS    XL1                                                      00261
DMDTRSV3 DS    XL1             RESERVED                                 00251
DMDTTRNC DS    XL4                                                      00261
DMDTTRNT DS    XL4                                                      00261
DMDTTWET DS    XL4                                                      00261
DMDTLO   DS    XL2             MIN  MPL LEVEL                           00252
DMDTHI   DS    XL2             MAX  MPL LEVEL                           00253
DMDTASRL DS    XL4             LOW AVG SERVICE RATE                     00253
DMDTASRH DS    XL4             HI  AVG SERVICE RATE                     00253
DMDTDSRL DS    XL4             LO  TOT SERVICE RATE                     00253
DMDTDSRH DS    XL4             HI  TOT SERVICE RATE                     00253
DMDTCRTI DS    XL2             ESTOR CRITERIA TABLE INDEX               00254
DMDTCRTR DS    XL2             REQUESTED CRIYERIA TABLE INDEX           00255
DMDTRUMW DS    XL2             WEIGHTED READY USER MAX                  00257
DMDTRSV5 DS    XL2             RESERVED                                 00261
DMDTWORK DS    XL100           WORKAREA                                 00261
DMDTEND  DS    0X              END OF DMT                               00262
DMDTLEN  EQU   *-DMDT                                                   00263
         SPACE 2                                                        00264
         CVT DSECT=YES,LIST=NO
         LTORG
         END
//*KED.SYSLMOD DD DSN=SYS1.MISC.LINKLIB(DDMN),DISP=SHR
//L.SYSLMOD DD DSN=TEST.PGM.LOAD(DDMN),DISP=SHR
